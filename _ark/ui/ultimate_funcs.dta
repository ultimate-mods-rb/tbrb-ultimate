{func
   init_dta_writer
   {write_file $ulti_settings_path
      {array
         (
            (gameplay_settings
               (song_speed $speedmod)
               (whammy_pitch_bending $whammypb)
               (disable_miss_sounds $nomisounds)
               (pad_is_guitar $padtar)
               (pad_is_drum $paddrum)
               (second_kick_mapping $secondkick)
               (mic_gain ({profile_mgr get_mic_gain 0} {profile_mgr get_mic_gain 1} {profile_mgr get_mic_gain 2}))
               (default_difficulties ($default_diff0 $default_diff1 $default_diff2 $default_diff3))
            )
            (graphics_settings
               (disable_all_post_fx $postfx)
               (black_bg $blackbg)
               (bloom $scrbloom)
               (camera_shake $camerashake)
               (depth_of_field $fxdof)
               (film_grain $scrnoise)
               (motion_blur $mbblend)
               (force_high_lod $forcelod0)
               (venue_fps $emulatefps)
               (vsync_rate $vsyncrate)
            )
            (track_settings
               (track_speed $trackspeed)
               (sync_difficulty_speeds $syncdifspeeds)
               (legacy_bass_streak $legacybass)
               (note_color_shuffle $colorshuffle)
               (guitar_gem_colors ($gtrgem1 $gtrgem2 $gtrgem3 $gtrgem4 $gtrgem5))
               (guitar_smasher_colors ($gtrsm1 $gtrsm2 $gtrsm3 $gtrsm4 $gtrsm5))
               (guitar_sustain_colors ($gtrsus1 $gtrsus2 $gtrsus3 $gtrsus4 $gtrsus5))
               (drum_gem_colors ($drmgem1 $drmgem2 $drmgem3 $drmgem4 $drmgem5))
               (drum_smasher_colors ($drms1 $drms2 $drms3 $drms4))
            )
            (track_textures
               (highway_texture $highway)
               (streak_texture $streak)
               (spotlight_texture $spotlight)
            )
            (ui_settings
               (disable_net_warnings $skipnetwarning)
               (show_song_author $showauthor)
               (skip_intro_video $skipintro)
               (skip_splash_screen $skipsplash)
               (song_title_always $songtitle)
            )
            (venue_settings
               (force_outfits $forceoutfit)
               (force_venue $forcevenue)
               (force_minivenue $forceminivenue)
               (force_scene $forcescene)
               (random_venue $randomvenue)
               (random_outfits $randomoutfit)
            )
         )
      }
   }
}
{func
   game_exit_handler
   {if_else
      {== {meta_performer get_venue} dreamscape}
      {if_else
         {'||'
            {!= $forcescene off}
            {!= $forceminivenue off}
            $ngskillissue
            
         }
         {script_task
            kTaskSeconds
            (delay 2)
            (script
               {$this game_outro}
            )
         } 
         kDataUnhandled
      }
      kDataUnhandled
   }
}

#ifdef HX_WII
{func ;this is a little hacky i'd say find a better way of doing this but i tried like 100 and this was the most reliable lol
   exec_song_scripting
   #include ../ulti/song_updates/song_scripting.dta
}
{func
   enable_wiimote_mapping
   {set_elem {find $syscfg beatmatcher controller wii_remote slots} 1 kPad_Circle}
   {set_elem {find $syscfg beatmatcher controller wii_remote slots} 3 kPad_DRight}
   {set_elem {find $syscfg beatmatcher controller wii_remote slots} 5 kPad_X}
   {set_elem {find $syscfg beatmatcher controller wii_remote slots} 7 kPad_Square}
   {set_elem {find $syscfg beatmatcher controller wii_remote slots} 9 kPad_Tri}
   {set_elem {find $syscfg beatmatcher controller wii_remote force_mercury} 1 kPad_DLeft}
}
{func
   disable_wiimote_mapping
   {set_elem {find $syscfg beatmatcher controller wii_remote slots} 1 6}
   {set_elem {find $syscfg beatmatcher controller wii_remote slots} 3 5}
   {set_elem {find $syscfg beatmatcher controller wii_remote slots} 5 4}
   {set_elem {find $syscfg beatmatcher controller wii_remote slots} 7 7}
   {set_elem {find $syscfg beatmatcher controller wii_remote slots} 9 2}
   {set_elem {find $syscfg beatmatcher controller wii_remote force_mercury} 1 kPad_Select}
}
#endif
{func
   enable_second_kick_mapping
   #ifdef HX_XBOX
   {insert_elems {find $syscfg beatmatcher controller hx_drums_xbox slots} 1 (kPad_R1 0)}
   {insert_elems {find $syscfg beatmatcher controller hx_drums_xbox_rb2 slots} 1 (kPad_R1 0)}
   #endif
   #ifdef HX_PS3
   {insert_elems {find $syscfg beatmatcher controller hx_drums_ps3 slots} 1 (kPad_R1 0)}
   #endif
   #ifdef HX_WII
   {insert_elems {find $syscfg beatmatcher controller wii_drums slots} 1 (kPad_R1 0)}
   #endif
}
{func
   disable_second_kick_mapping
   #ifdef HX_XBOX
   {set_elem {find $syscfg beatmatcher controller hx_drums_xbox slots} 1 69}
   {set_elem {find $syscfg beatmatcher controller hx_drums_xbox_rb2 slots} 1 69}
   #endif
   #ifdef HX_PS3
   {set_elem {find $syscfg beatmatcher controller hx_drums_ps3 slots} 1 69}
   #endif
   #ifdef HX_WII
   {set_elem {find $syscfg beatmatcher controller wii_drums slots} 1 69}
   #endif
}